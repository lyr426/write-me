buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.commonmark:commonmark:0.21.0'
    }
}

import org.commonmark.parser.Parser
import org.commonmark.renderer.html.HtmlRenderer

plugins {
    id 'java'
    id 'org.jetbrains.intellij.platform' version '2.2.1'
}

group = providers.gradleProperty('pluginGroup').get()
version = providers.gradleProperty('pluginVersion').get()

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    intellijPlatform {
        create(
                providers.gradleProperty('platformType'),
                providers.gradleProperty('platformVersion')
        )

        bundledPlugins('com.intellij.java', 'Git4Idea')

        pluginVerifier()
        zipSigner()
        instrumentationTools()
    }

    testImplementation 'junit:junit:4.13.2'
    implementation 'org.json:json:20250107'
}

intellijPlatform {
    pluginConfiguration {
        id = providers.gradleProperty('pluginGroup').map { it + ".write-me" }
        name = providers.gradleProperty('pluginName')
        version = providers.gradleProperty('pluginVersion')

        ideaVersion {
            sinceBuild = providers.gradleProperty('pluginSinceBuild')
            untilBuild = providers.gradleProperty('pluginUntilBuild')
        }
    }
}

tasks.runIde {
    jvmArgs("-Djava.security.manager=disallow")
}

tasks.patchPluginXml {
    pluginDescription = provider {
        def file = file("README.md")
        if (!file.exists()) return "README not found"

        def text = file.text
        def start = ""
        def end = ""

        def startIndex = text.indexOf(start)
        def endIndex = text.indexOf(end)

        def content = ""
        if (startIndex >= 0 && endIndex > startIndex) {
            content = text.substring(startIndex + start.length(), endIndex).trim()
        } else {
            content = text
        }

        Parser parser = Parser.builder().build()
        HtmlRenderer renderer = HtmlRenderer.builder().build()
        return renderer.render(parser.parse(content))
    }
}
